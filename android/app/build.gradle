plugins {
    id "com.android.application"
    id 'com.google.gms.google-services'
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

// ì‹œí¬ë¦¿ íŒŒì¼ ê²½ë¡œ íƒìƒ‰ (ìš°ì„ ìˆœìœ„ ìˆœ)
def findSecretsDir() {
    def candidates = [
        System.getenv('SECRETS_PATH'),                              // 1. í™˜ê²½ë³€ìˆ˜
        rootProject.file('../secrets').absolutePath,                // 2. í”„ë¡œì íŠ¸ ë£¨íŠ¸/secrets (ì¼ë°˜ í´ë¡ )
        rootProject.file('../../../secrets').absolutePath,          // 3. Conductor ì›Œí¬ìŠ¤í˜ì´ìŠ¤
    ]
    for (path in candidates) {
        if (path != null && new File(path).exists()) {
            println "ğŸ“ Using secrets from: ${path}"
            return new File(path)
        }
    }
    // fallback: ê¸°ì¡´ ìœ„ì¹˜ (android/)
    return rootProject.file('.')
}

def secretsDir = findSecretsDir()
def keystoreProperties = new Properties()
def keystorePropertiesFile = new File(secretsDir, 'key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    println "ğŸ”‘ Loaded key.properties from: ${keystorePropertiesFile.absolutePath}"
} else {
    println "âš ï¸  key.properties not found. Release build will fail."
    println "   Create secrets/key.properties in project root."
}

android {
    namespace = "com.bottlenote.official.app"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    defaultConfig {
        applicationId = "com.bottlenote.official.app"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        
        resValue "string", "KAKAO_NATIVE_APP_KEY", "\"kakao${System.getenv('KAKAO_NATIVE_APP_KEY')}\""
    }

    dependencies {
    // For AGP 7.4+
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    // For AGP 7.3
    // coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.3'
    // For AGP 4.0 to 7.2
    // coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.9'
    }


/* ê¸°ì¡´ ë¹Œë“œ íƒ€ì… ì„¤ì •
    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.debug
        }
    }
*/
    // ë³€ê²½ëœ ë¹Œë“œ íƒ€ì… ì„¤ì •
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            // storeFile ê²½ë¡œë¥¼ secrets í´ë” ê¸°ì¤€ìœ¼ë¡œ í•´ì„
            def storeFilePath = keystoreProperties['storeFile']
            if (storeFilePath) {
                def storeFileObj = new File(storeFilePath)
                // ì ˆëŒ€ê²½ë¡œê°€ ì•„ë‹ˆë©´ secrets í´ë” ê¸°ì¤€ìœ¼ë¡œ í•´ì„
                storeFile = storeFileObj.isAbsolute() ? storeFileObj : new File(secretsDir, storeFilePath)
            }
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    // Android 16KB í˜ì´ì§€ ì‚¬ì´ì¦ˆ ì§€ì›
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
}

flutter {
    source = "../.."
}


